document = ${
  preamble ~ NEWLINE ~
  (COMMENT | empty)* ~
  (plan ~ body | body ~ plan)
}
preamble = !{ ^"tap version" ~ version }

version = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)* }

plan = ${ first ~ ".." ~ last ~ WHITESPACE* ~ ("#" ~ WHITESPACE* ~ reason)? }
first = @{ ASCII_DIGIT+ }
last = @{ ASCII_DIGIT+ }
reason = { (inactive_char)+ }

body = { statement* }
statement = _{
  empty |
  subtest |
  test |
  bail_out |
  pragma |
  anything |
  COMMENT
}

test = ${
  result ~ WHITESPACE* ~ number? ~
  (WHITESPACE* ~ "-"? ~ WHITESPACE* ~ description)? ~
  WHITESPACE* ~ directive? ~ WHITESPACE* ~ COMMENT? ~
  (NEWLINE ~ yaml_block)?
}
result = @{ ^"not "? ~ ^"ok" }
number = @{ ASCII_DIGIT+ }
key = @{ ^"todo" | ^"skip" }
description = { !directive ~ inactive_char+ }
directive = ${ "#" ~ WHITESPACE* ~ key ~ (WHITESPACE+ ~ reason)? }

bail_out = { ^"bail out!" ~ reason? }

pragma = { ^"pragma " ~ ("+" | "-") ~ pragma_key ~ NEWLINE? }
pragma_key = { ASCII_ALPHANUMERIC+ ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

subtest = ${
  subtest_decl? ~
  empty* ~
  (
    PUSH(WHITESPACE+) ~ plan ~
    (COMMENT | empty | (PEEK_ALL ~ statement))*
    |
    PUSH(WHITESPACE+) ~ statement ~
    (!(PEEK_ALL ~ plan) ~ (COMMENT | empty | (PEEK_ALL ~ statement)))* ~
    PEEK_ALL ~ plan
  ) ~ DROP
}
subtest_decl = _{ "#" ~ WHITESPACE* ~ ^"subtest" ~ (":" ~ WHITESPACE* ~ name)? ~ NEWLINE }
name = @{ inactive_char* }

yaml_block = ${
  PUSH(WHITESPACE+) ~ yaml_start ~ WHITESPACE* ~ NEWLINE ~
  (PEEK ~ yaml ~ NEWLINE)* ~
  PEEK ~ yaml_end ~ DROP
}
yaml_start = _{ "---" }
yaml = { !yaml_end ~ (inactive_char | "#")* }
yaml_end = _{ "..." }

inactive_char = _{ (LETTER | MARK | NUMBER | SYMBOL | SEPARATOR | ' '..'"' | '%'..'@') } // pretty much everything except '#'

anything = { (!plan ~ inactive_char)+ }

empty = _{ WHITESPACE* ~ NEWLINE }

WHITESPACE = _{ " " | "\t" | "\r" }
COMMENT = _{ !subtest_decl ~ "#" ~ (!NEWLINE ~ inactive_char*)? }
