document = ${
  ^"tap version" ~ WHITESPACE+ ~ version ~ NEWLINE ~
  (COMMENT | empty)* ~
  (plan ~ body | body ~ plan)
}

version = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)* }

plan = { first ~ ".." ~ last ~ WHITESPACE* ~ ("#" ~ WHITESPACE* ~ reason)? ~ NEWLINE? }
first = @{ ASCII_DIGIT+ }
last = @{ ASCII_DIGIT+ }
reason = { (!NEWLINE ~ ANY)+ }

body = { statement* }
statement = _{ empty | subtest | (COMMENT | test | bail_out | pragma | anything) ~ (NEWLINE | EOI) }

test = ${ result ~ WHITESPACE* ~ number? ~ (WHITESPACE* ~ "-"? ~ WHITESPACE* ~ description)? ~ WHITESPACE* ~ directive? ~ WHITESPACE* ~ COMMENT? }
result = @{ ^"not "? ~ ^"ok" }
number = @{ ASCII_DIGIT+ }
key = @{ ^"todo" | ^"skip" }
description = { !directive ~ text }
directive = ${ "#" ~ WHITESPACE* ~ key ~ (WHITESPACE+ ~ reason)? }

bail_out = { ^"bail out!" ~ reason? }

pragma = { ^"pragma " ~ ("+" | "-") ~ pragma_key ~ NEWLINE? }
pragma_key = { ASCII_ALPHANUMERIC+ ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

subtest = ${
  subtest_decl? ~
  empty* ~
  (
    PUSH(WHITESPACE+) ~ plan ~
    (COMMENT | empty | (PEEK_ALL ~ statement))*
    |
    PUSH(WHITESPACE+) ~ statement ~
    (COMMENT | empty | (PEEK_ALL ~ statement))* ~
    PEEK_ALL ~ plan
  ) ~ DROP
}
subtest_decl = _{ "#" ~ WHITESPACE* ~ ^"subtest" ~ (":" ~ WHITESPACE* ~ name)? ~ NEWLINE }
name = @{ text }

inactive_char = _{ (LETTER | MARK | NUMBER | SYMBOL | SEPARATOR | ' '..'"' | '%'..'@') } // pretty much everything except '#'

text = _{ inactive_char* }

anything = { (!(NEWLINE | EOI) ~ inactive_char)+ }

empty = _{ WHITESPACE* ~ NEWLINE }

WHITESPACE = _{ " " | "\t" | "\r" }
COMMENT = _{ !subtest_decl ~ "#" ~ (!NEWLINE ~ text)? }
