document = { version_declaration ~ NEWLINE+ ~ plan ~ NEWLINE+ ~ body }

version_declaration = _{ ^"TAP" ~ ^"version" ~ version }
version = { ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)* }

plan = { first_test ~ ".." ~ last_test ~ ("#" ~ reason)? }
first_test = @{ ASCII_DIGIT+ }
last_test = @{ ASCII_DIGIT+ }
reason = { (!NEWLINE ~ ANY)+ }

body = { statement* }
statement = _{ test | bail_out | pragma | subtest | comment1 | anything | empty }
test = { result ~ number? ~ ("-"? ~ description)? ~ directive? ~ NEWLINE* }
result = { ^"not"? ~ ^"ok" }
number = @{ ASCII_DIGIT+ }
key = { ^"todo" | ^"skip" }
description = { (LETTER | NUMBER | SYMBOL)+ ~ (!(directive) ~ (LETTER | NUMBER | PUNCTUATION | SYMBOL)+)? }
directive = ${ "#" ~ " "* ~ key ~ (" "+ ~ reason)? }

bail_out = { ^"bail out!" ~ reason? ~ NEWLINE* }

pragma = { ^"pragma " ~ ("+" | "-") ~ pragma_key ~ NEWLINE* }
pragma_key = { ASCII_ALPHANUMERIC+ ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

subtest = @{
    ("#" ~ WHITESPACE* ~ ^"subtest" ~ (":" ~ WHITESPACE* ~ subtest_name?)?)? ~ NEWLINE
    ~ indent ~ plan ~ NEWLINE
    ~ (indent ~ (!NEWLINE ~ ANY)* ~ NEWLINE)*
}
subtest_name = { (LETTER | NUMBER | SYMBOL | WHITESPACE)+ }
indent = _{ " "{4} }

comment1 = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE* }

anything = { (!NEWLINE ~ ANY)+ ~ NEWLINE? }

empty = _{ WHITESPACE* ~ NEWLINE }

WHITESPACE = _{ " " | "\t" | "\r" }
